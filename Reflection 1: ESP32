I started coding the ESP32 for our project
NOTES: There are still some changes to be made.
1 CLAP FIRST TIME= LED ON
1 CLAP SECOND TIME = LED OFF
2 CLAPS = BRIGHTNESS AT 120
3 CLAPS BRIGHTNESS AT 40

CODE:

const int MIC_PIN = 34;
const int LED_PIN = 33;     
const int SMOOTH_SAMPLES = 15;
int noiseFloor = 0;
int CLAP_THRESHOLD = 150;

//Timing 
const unsigned long MIN_GAP = 70;   
const unsigned long COOLDOWN = 100;  
const unsigned long SEQ_TIMEOUT = 1000; 

//Clap detection
unsigned long lastPeakTime = 0;
bool inCooldown = false;

//sensor values
unsigned long lastPrint = 0;
const unsigned long PRINT_INTERVAL = 60;

//PWM
const int PWM_CH = 0;

// LED toggle state
bool ledState = false;

int clapCount = 0;
unsigned long seqStart = 0;
unsigned long lastClapTime = 0;

int smoothRead(int pin) {
  long s = 0;
  for (int i = 0; i < SMOOTH_SAMPLES; ++i) {
    s += analogRead(pin);
  }
  return s / SMOOTH_SAMPLES;
}

int calibrateNoise(int pin, int samples = 200, int delayMs = 3) {
  long s = 0;
  for (int i = 0; i < samples; ++i) {
    s += smoothRead(pin);
    delay(delayMs);
  }
  return (int)(s / samples);
}

bool isPeak(int value) {
  return value >= CLAP_THRESHOLD;
}

void setupLED() {
  ledcSetup(PWM_CH, 5000, 8);
  ledcAttachPin(LED_PIN, PWM_CH);
  ledcWrite(PWM_CH, 0);
}

void setLEDBrightness(int b) {
  b = constrain(b, 0, 255);
  ledcWrite(PWM_CH, b);
}

void setup() {
  Serial.begin(115200);
  delay(300);
  pinMode(MIC_PIN, INPUT);
  noiseFloor = calibrateNoise(MIC_PIN);
  setupLED();
}

void loop() {
  unsigned long now = millis();

  //mic values 
  if (now - lastPrint >= PRINT_INTERVAL) {
    int val = smoothRead(MIC_PIN);
    Serial.print("Mic: "); Serial.println(val);
    lastPrint = now;
  }

  int micVal = smoothRead(MIC_PIN);

  if (inCooldown) {
    if (micVal < CLAP_THRESHOLD * 0.6) {
      inCooldown = false;
    }
  }

  if (!inCooldown && isPeak(micVal) && (now - lastPeakTime) > MIN_GAP) {

    if (clapCount == 0) {
      seqStart = now;
    }

    clapCount++;
    lastClapTime = now;
    lastPeakTime = now;
    inCooldown = true;

    Serial.print("Clap detected. Count = ");
    Serial.println(clapCount);
  }

  if (clapCount > 0 && (now - lastClapTime) > SEQ_TIMEOUT) {

    int pattern = 0;

    if (clapCount == 1) {
      pattern = ledState ? 1 : 2;
    }
    else if (clapCount == 2) {
      pattern = 3;
    }
    else if (clapCount == 3) {
      pattern = 4;
    }

    Serial.print("Final pattern detected: ");
    Serial.println(pattern);

    // LED ACTIONS
    if (pattern == 1) {
      Serial.println("Pattern 1 = LED OFF");
      setLEDBrightness(0);
      ledState = false;
    }
    else if (pattern == 2) {
      Serial.println("Pattern 2 = LED ON");
      setLEDBrightness(255);
      ledState = true;
    }
    else if (pattern == 3) {
      Serial.println("Pattern 3 = BRIGHTNESS_SET (120)");
      setLEDBrightness(120);
      ledState = true;
    }
    else if (pattern == 4) {
      Serial.println("Pattern 4 = BRIGHTNESS_SET (40)");
      setLEDBrightness(40);
      ledState = true;
    }

    // RESET 
    clapCount = 0;
  }
  
  delay(10);
}
